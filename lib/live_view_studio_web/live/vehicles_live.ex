defmodule LiveViewStudioWeb.VehiclesLive do
  use LiveViewStudioWeb, :live_view

  alias LiveViewStudio.Vehicles

  def mount(_params, _session, socket) do
    socket =
      assign(socket,
        make_or_model: "",
        vehicles: [],
        loading: false,
        matches: %{}
      )

    {:ok, socket}
  end

  def render(assigns) do
    ~H"""
    <h1>ðŸš™ Find a Vehicle ðŸš˜</h1>
    <div id="vehicles">
      <form phx-change="suggest" phx-submit="search">
        <input
          type="text"
          name="query"
          value={@make_or_model}
          placeholder="Make or model"
          autofocus
          autocomplete="off"
          readonly={@loading}
          list="matches"
          phx-debounce="250"
        />

        <button>
          <img src="/images/search.svg" />
        </button>
      </form>

      <datalist id="matches">
        <option :for={vehicle <- @matches} value={vehicle}>
          <%= vehicle %>
        </option>
      </datalist>

      <.loading visible={@loading} />

      <div class="vehicles">
        <ul>
          <li :for={vehicle <- @vehicles}>
            <span class="make-model">
              <%= vehicle.make_model %>
            </span>
            <span class="color">
              <%= vehicle.color %>
            </span>
            <span class={"status #{vehicle.status}"}>
              <%= vehicle.status %>
            </span>
          </li>
        </ul>
      </div>
    </div>
    """
  end

  def handle_event("suggest", %{"query" => prefix}, socket) do
    matches = Vehicles.suggest(prefix)
    {:noreply, assign(socket, matches: matches)}
  end

  def handle_event("search", %{"query" => make_or_model}, socket) do
    send(self(), {:run_search, make_or_model})

    socket =
      assign(socket,
        make_or_model: make_or_model,
        vehicles: [],
        loading: true
      )

    {:noreply, socket}
  end

  def handle_info({:run_search, make_or_model}, socket) do
    vehicles = Vehicles.search(make_or_model)
    {:noreply, assign(socket, vehicles: vehicles, loading: false)}
  end
end
